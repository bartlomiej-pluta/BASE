plugins {
    id 'java-library'
    id "com.google.protobuf" version "$protobufPluginVersion"
}

group 'com.bartlomiejpluta.base'
version 'unspecified'

repositories {
    mavenCentral()
}

dependencies {
    api "com.google.protobuf:protobuf-java:$protobufVersion"
}

test {
    useJUnitPlatform()
}

sourceSets {
    main {
        java {
            srcDirs "$projectDir/build/proto/main/java"
        }
    }
}

protobuf {
    // Fetch protoc compiler
    protoc {
        def protobufExecutable = System.getenv("PROTOBUF_EXECUTABLE")

        if (protobufExecutable == null) {
            artifact = "com.google.protobuf:protoc:$protobufVersion"
        } else {
            path = protobufExecutable
        }
    }

    generatedFilesBaseDir = "$projectDir/build/proto/"

    // Enable compiling proto files to Java
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { }
            }
        }
    }
}
