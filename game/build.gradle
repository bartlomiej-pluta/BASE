plugins {
    id 'org.springframework.boot' version "$springBootVersion"
    id 'io.spring.dependency-management' version "$springDependencyManagementVersion"
    id 'java'
    id 'application'
}

group 'com.bartlomiejpluta.base'
version 'unspecified'

import org.gradle.internal.os.OperatingSystem

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64")
                ? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}"
                : "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = System.getProperty("os.arch").contains("64") ? "natives-windows" : "natives-windows-x86"
        break
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation project(":engine")
    implementation project(":proto")

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    // LWJGL
    implementation "org.lwjgl:lwjgl-glfw"

    // Spring
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // This dependency is used by the application.
    implementation "com.google.guava:guava:${guavaVersion}"
    implementation "org.joml:joml:${jomlVersion}"
}

application {
    // Define the main class for the application.
    mainClass = 'com.bartlomiejpluta.base.game.App'
}

build {
    dependsOn(":proto:build")
}
